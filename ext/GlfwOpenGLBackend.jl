module GlfwOpenGLBackend

import CSyntax: @c
import CImGui as ig
import CImGui.lib as lib
import GLFW
import ModernGL as GL

using PrecompileTools: @compile_workload

# because pkgversion do not work with PackageCompiler
const GLFW_VERSION = pkgversion(GLFW)

# Helper function to get the GLSL version
function get_glsl_version(gl_version)
    gl2glsl = Dict(v"2.0" => 110,
                   v"2.1" => 120,
                   v"3.0" => 130,
                   v"3.1" => 140,
                   v"3.2" => 150)

    if gl_version < v"3.3"
        gl2glsl[gl_version]
    else
        gl_version.major * 100 + gl_version.minor * 10
    end
end

const g_ImageTexture = Dict{Int, GL.GLuint}()

function ig._create_image_texture(::Val{:GlfwOpenGL3}, image_width, image_height; format=GL.GL_RGBA, type=GL.GL_UNSIGNED_BYTE, filter=GL.GL_LINEAR)
    id = GL.GLuint(0)
    @c GL.glGenTextures(1, &id)
    GL.glBindTexture(GL.GL_TEXTURE_2D, id)
    GL.glTexParameteri(GL.GL_TEXTURE_2D, GL.GL_TEXTURE_MIN_FILTER, filter)
    GL.glTexParameteri(GL.GL_TEXTURE_2D, GL.GL_TEXTURE_MAG_FILTER, filter)
    GL.glPixelStorei(GL.GL_UNPACK_ROW_LENGTH, 0)
    GL.glTexImage2D(GL.GL_TEXTURE_2D, 0, format, GL.GLsizei(image_width), GL.GLsizei(image_height), 0, format, type, C_NULL)
    g_ImageTexture[id] = id
    return ig.ImTextureRef(ig.ImTextureID(id))
end

function ig._update_image_texture(::Val{:GlfwOpenGL3}, tex_ref, image_data, image_width, image_height; format=GL.GL_RGBA, type=GL.GL_UNSIGNED_BYTE)
    id = tex_ref._TexID
    if id <= 0
        error("ImTextureRef has an invalid ImTextureId: $(id)")
    end

    GL.glBindTexture(GL.GL_TEXTURE_2D, g_ImageTexture[id])
    GL.glTexSubImage2D(GL.GL_TEXTURE_2D, 0, 0, 0, GL.GLsizei(image_width), GL.GLsizei(image_height), format, type, image_data)
end

function ig._destroy_image_texture(::Val{:GlfwOpenGL3}, tex_ref)
    id = tex_ref._TexID
    if id <= 0
        error("ImTextureRef has an invalid ImTextureId: $(id)")
    end

    id = g_ImageTexture[id]
    @c GL.glDeleteTextures(1, &id)
    delete!(g_ImageTexture, id)
    return true
end

_window::Union{Nothing, GLFW.Window} = nothing
ig._current_window(::Val{:GlfwOpenGL3}) = _window

# test engine drives screenshots through a backend-provided `ImGuiScreenCaptureFunc`:
# bool (*)(ImGuiID viewport_id, int x, int y, int w, int h, unsigned int* pixels, void* user_data)
#
# It must write the framebuffer region (x, y, w, h) into `pixels` as RGBA8
# capture tool works in ImGui display coordinates (logical pixels), so on a HiDPI display
# the actual framebuffer is larger and we downsample
function _capture_framebuffer(viewport_id::lib.ImGuiID, x::Cint, y::Cint, w::Cint, h::Cint,
                              pixels::Ptr{Cuint}, user_data::Ptr{Cvoid})::Bool
    (w <= 0 || h <= 0) && return true

    window = _window
    window === nothing && return false

    # derive the logical->framebuffer scale
    fb_w, fb_h = GLFW.GetFramebufferSize(window)
    io = ig.GetIO()
    display = unsafe_load(io.DisplaySize)
    sx = fb_w / display.x
    sy = fb_h / display.y

    # The framebuffer region corresponding to the requested logical rect.
    fx = round(Int, x * sx)
    fw = round(Int, w * sx)
    fh = round(Int, h * sy)
    # GL's origin is bottom-left, so flip the rect's y to GL coordinates.
    gl_y = fb_h - round(Int, (y + h) * sy)

    # Read the front buffer, restoring the previously-bound read buffer after.
    prev_read_buffer = Ref{GL.GLint}(0)
    GL.glGetIntegerv(GL.GL_READ_BUFFER, prev_read_buffer)
    GL.glReadBuffer(GL.GL_FRONT)
    GL.glPixelStorei(GL.GL_PACK_ALIGNMENT, 1)

    # glReadPixels returns rows bottom-to-top, fw wide, fh tall.
    scratch = Vector{Cuint}(undef, fw * fh)
    GC.@preserve scratch begin
        GL.glReadPixels(fx, gl_y, fw, fh, GL.GL_RGBA, GL.GL_UNSIGNED_BYTE, pointer(scratch))
    end
    GL.glReadBuffer(prev_read_buffer[])

    # Downsample (nearest) into the logical w×h output, flipping rows: GL rows are
    # bottom-to-top, the tool wants top-to-bottom (the `fh -` does the flip).
    S = reshape(scratch, fw, fh)
    cols = clamp.(round.(Int, ((1:w) .- 0.5) .* sx), 0, fw - 1) .+ 1
    rows = fh .- clamp.(round.(Int, ((1:h) .- 0.5) .* sy), 0, fh - 1)
    out = unsafe_wrap(Array, pixels, (w, h))   # view the caller's buffer, no copy
    out .= @view S[cols, rows]

    return true
end

# @cfunction pointer should be built in __init__, not during precompilation
_CAPTURE_CFN::Ptr{Cvoid} = C_NULL

function __init__()
    global _CAPTURE_CFN = @cfunction(_capture_framebuffer, Bool,
                                     (lib.ImGuiID, Cint, Cint, Cint, Cint, Ptr{Cuint}, Ptr{Cvoid}))
end

function renderloop(ui, ctx::Ptr{lib.ImGuiContext}, ::Val{:GlfwOpenGL3};
                    hotloading=true,
                    on_exit=Returns(nothing),
                    clear_color=Cfloat[0.45, 0.55, 0.60, 1.00],
                    window_size=(1280, 720),
                    window_title="CImGui",
                    engine=nothing,
                    opengl_version=v"3.2",
                    wait_events=false)
    if GLFW_VERSION >= v"3.4.4"
        # We leave thread-safety to the user
        GLFW.ENABLE_THREAD_ASSERTIONS[] = false
    end

    # Validate arguments
    if clear_color isa Ref && !isassigned(clear_color)
        throw(ArgumentError("'clear_color' is a unassigned reference, it must be initialized properly."))
    elseif Sys.isapple() && opengl_version < v"3.2"
        throw(ArgumentError("Only OpenGL 3.2+ is supported on OSX, but $(opengl_version) was requested"))
    end

    # Configure GLFW
    glsl_version = get_glsl_version(opengl_version)
    GLFW.WindowHint(GLFW.VISIBLE, true)
    GLFW.WindowHint(GLFW.DECORATED, true)
    GLFW.WindowHint(GLFW.CONTEXT_VERSION_MAJOR, opengl_version.major)
    GLFW.WindowHint(GLFW.CONTEXT_VERSION_MINOR, opengl_version.minor)

    if Sys.isapple()
        GLFW.WindowHint(GLFW.OPENGL_PROFILE, GLFW.OPENGL_CORE_PROFILE)
        GLFW.WindowHint(GLFW.OPENGL_FORWARD_COMPAT, GL.GL_TRUE)
    end

    # Start the test engine, if we have one
    if !isnothing(engine)
        ig._start_test_engine(engine, ctx)
    end

    # Create window
    global _window = GLFW.CreateWindow(window_size[1], window_size[2], window_title)
    window = _window
    @assert window != C_NULL
    GLFW.MakeContextCurrent(window)
    GLFW.SwapInterval(1)  # enable vsync

    # Setup Platform/Renderer bindings
    lib.ImGui_ImplGlfw_InitForOpenGL(Ptr{lib.GLFWwindow}(window.handle), true)
    lib.ImGui_ImplOpenGL3_Init("#version $(glsl_version)")

    # screen-capture function so the test engine can take screenshots
    # here (not in ImGuiTestEngine) because the capture needs the GL context
    if !isnothing(engine)
        engine.IO.ScreenCaptureFunc = _CAPTURE_CFN
    end

    try
        while !GLFW.WindowShouldClose(window)
            # Polling/waiting for events blocks the whole thread and there's
            # nothing we can do about it. But, if there are no callbacks
            # registered on the window we can safely enter a GC-safe region to
            # allow the GC to run in parallel, which will help prevent GC pauses
            # that cause the GUI to stutter.
            have_callbacks = any(!isnothing, GLFW.callbacks(window))
            if have_callbacks
                wait_events ? GLFW.WaitEvents() : GLFW.PollEvents()
            else
                try
                    @ccall jl_gc_safe_enter()::Int8
                    wait_events ? GLFW.WaitEvents() : GLFW.PollEvents()
                finally
                    @ccall jl_gc_safe_leave()::Int8
                end
            end

            # Start the Dear ImGui frame
            lib.ImGui_ImplOpenGL3_NewFrame()
            lib.ImGui_ImplGlfw_NewFrame()
            ig.NewFrame()

            result = if hotloading
                @invokelatest ui()
            else
                ui()
            end

            if !isnothing(engine) && engine.show_test_window
                ig._show_test_window(engine)
            end

            tests_completed = (!isnothing(engine)
                               && engine.exit_on_completion
                               && !ig._test_engine_is_running(engine))
            if result === :imgui_exit_loop || tests_completed
                GLFW.SetWindowShouldClose(window, true)
            end

            # Rendering
            ig.Render()
            GLFW.MakeContextCurrent(window)

            display_w, display_h = GLFW.GetFramebufferSize(window)

            GL.glViewport(0, 0, display_w, display_h)
            GL.glClearColor((clear_color isa Ref ? clear_color[] : clear_color)...)
            GL.glClear(GL.GL_COLOR_BUFFER_BIT)
            lib.ImGui_ImplOpenGL3_RenderDrawData(Ptr{Cint}(ig.GetDrawData()))

            GLFW.MakeContextCurrent(window)

            # This is when what we've just rendered actually gets
            # displayed. With the swap interval set to 1 it will synchronize
            # with the display refresh rate to prevent screen tearing, which
            # means this may block for a non-negligible amount of time. Hence we
            # again enter a GC-safe region so as not to block the
            # GC. SwapBuffers() does not call any callbacks so this should
            # always be safe.
            try
                @ccall jl_gc_safe_enter()::Int8
                GLFW.SwapBuffers(window)
            finally
                @ccall jl_gc_safe_leave()::Int8
            end

            # Advance test engine's screen-capture state.
            if !isnothing(engine)
                ig._post_swap(engine)
            end

            if (unsafe_load(ig.GetIO().ConfigFlags) & lib.ImGuiConfigFlags_ViewportsEnable) == lib.ImGuiConfigFlags_ViewportsEnable
                backup_current_context = GLFW.GetCurrentContext()
                lib.igUpdatePlatformWindows()
                lib.igRenderPlatformWindowsDefault(C_NULL, C_NULL)
                GLFW.MakeContextCurrent(backup_current_context)
            end

            yield()
        end
    catch e
        @error "Error in CImGui $(ig._backend) renderloop!" exception=(e, catch_backtrace())
    finally
        for func in vcat(ig._exit_handlers, [on_exit])
            try
                func()
            catch ex
                @error "Error in CImGui.jl exit handler!" exception=(ex, catch_backtrace())
            end
        end

        lib.ImGui_ImplOpenGL3_Shutdown()
        lib.ImGui_ImplGlfw_Shutdown()
        ig.DestroyContext(ctx)
        GLFW.DestroyWindow(window)
    end
end

function ig._render(args...; spawn::Union{Bool, Integer, Symbol}=1, wait::Bool=true, kwargs...)
    if spawn === false
        return renderloop(args...; kwargs...)
    end

    # Note that when picking a thread from a threadpool automatically we always
    # take the last thread ID to try to avoid picking thread 1. Thread 1 is
    # kinda important because it runs the libuv eventloop, so spawning a
    # mostly-non-yielding loop on that could interfere with other Julia
    # tasks. It's also somewhat unsafe because GLFW might not play well on
    # anything other than thread 1, but the caller should be aware of that
    # already.
    t = @task renderloop(args...; kwargs...)
    if spawn === true
        pool = Threads.threadpoolsize(:interactive) == 0 ? :default : :interactive
        ig.pintask!(t, Threads.threadpooltids(pool)[end])
    elseif spawn isa Integer
        ig.pintask!(t, spawn)
    elseif spawn isa Symbol
        if Threads.threadpoolsize(spawn) == 0
            error("Threadpool '$spawn' is empty, cannot schedule the ImGui renderloop onto it.")
        end

        ig.pintask!(t, Threads.threadpooltids(spawn)[end])
    else
        throw(ArgumentError("Unrecognized `spawn` value: '$(spawn)'"))
    end

    schedule(t)
    monitor_task = errormonitor(t)
    return wait ? Base.wait(monitor_task) : monitor_task
end

@compile_workload begin
    ctx = ig.CreateContext()
    ig.set_backend(:GlfwOpenGL3)
    ig.render(ctx; window_title="CImGui precompile workload") do
        ig.Begin("Foo")
        ig.Text("foo")
        ig.End()

        return :imgui_exit_loop
    end

    ig._backend = nothing
end

end
